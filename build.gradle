buildscript {

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jacobono:gradle-jaxb-plugin:1.3.6'
    }

}

apply plugin: 'java'

allprojects {
    group = 'com.netflix.imfutility'
    version = '1.0'
}

project.ext {
    javaSubprojectList = subprojects

    xsdJavaSubprojectList = subprojects.findAll {
        it.name == 'dpp-conversion-input-xsd' ||
                it.name == 'imf-conversion-core' ||
                it.name == 'imf-validation'
    }

    testArtifactsJavaSubprojectList = subprojects.findAll {
        it.name == 'dpp-conversion' ||
                it.name == 'imf-conversion-core'
    }

}

configure(project.javaSubprojectList) {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
        compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.5'
        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.5'
        compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.5'
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

configure(project.xsdJavaSubprojectList) {
    apply plugin: 'com.github.jacobono.jaxb'

    System.setProperty('javax.xml.accessExternalSchema', 'file')

    dependencies {
        jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
        jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
        jaxb 'javax.xml.bind:jaxb-api:2.2.7'
        jaxb "org.jvnet.jaxb2_commons:jaxb2-basics-ant:0.6.5"
        jaxb "org.jvnet.jaxb2_commons:jaxb2-basics:0.6.4"
        jaxb "org.jvnet.jaxb2_commons:jaxb2-basics-annotate:0.6.4"
        jaxb "org.jvnet.jaxb2_commons:jaxb2-value-constructor:3.0"
    }

    jaxb {
        xsdDir = "${project.name}/src/main/resources/xsd"
        bindingsDir = "${project.name}/src/main/resources/xjb"
        bindings = ["**/*.xjb"]
        xjc {
            taskClassname = "org.jvnet.jaxb2_commons.xjc.XJC2Task"
            // generatePackages are specified in xjb bindings
            args = ["-Xinheritance", "-Xannotate"]
            destinationDir = "src/generated/java"
        }
    }

    sourceSets {
        main {
            java {
                srcDir 'src/generated/java'
            }
        }

        test {
            java {
                srcDir 'src/generated/java'
            }
        }

    }

    clean {
        delete 'src/generated'
        delete 'schema'
    }

    compileJava.dependsOn(xjc)
}

configure(project.testArtifactsJavaSubprojectList) {
    configurations {
        testArtifacts.extendsFrom testCompile
    }

    task jarTest(type: Jar, dependsOn: testClasses) {
        from sourceSets.test.output
        from sourceSets.test.resources
        classifier = 'test'
    }

    artifacts {
        testArtifacts jarTest
    }
}


